apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'

buildscript {
  ext.versions = [
    minSdk: 15,
    compileSdk: 27,
    targetSdk: 27,
    supportLibrary: '27.1.1',
    espresso: '3.0.2',
    testRules: '1.0.2',
    autoValue: '1.6',
  ]

  ext.deps = [
    'rx': [
      'java': 'io.reactivex.rxjava2:rxjava:2.1.14',
    ],
    'autoValue': [
      'compiler': "com.google.auto.value:auto-value:${versions.autoValue}",
      'runtime': "com.google.auto.value:auto-value-annotations:${versions.autoValue}",
    ],
    'support': [
      'appCompat': "com.android.support:appcompat-v7:${versions.supportLibrary}",

      'test': [
        'espresso': [
          'core': "com.android.support.test.espresso:espresso-core:${versions.espresso}",
          'web': "com.android.support.test.espresso:espresso-web:${versions.espresso}",
          'intents': "com.android.support.test.espresso:espresso-intents:${versions.espresso}",
        ],
        'runner': "com.android.support.test:runner:${versions.testRules}",
        'rules': "com.android.support.test:rules:${versions.testRules}",
        'uiautomator': 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3',
      ],
    ],
    'test': [
      'mockito': 'org.mockito:mockito-core:2.18.3',
      'junit': 'junit:junit:4.12',
      'assertj3': 'org.assertj:assertj-core:3.10.0',
    ],
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.10.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.11.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.2.0'

    classpath 'com.android.tools.build:gradle:3.1.2'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
  }
}

codeQualityTools {
  checkstyle {
    toolVersion = '8.7'
  }
  pmd {
    toolVersion = '6.0.1'
  }
  errorProne {
    toolVersion = '2.3.1'
  }
  lint {
    textReport = true
  }
}

subprojects {
  task allDependencies(type: DependencyReportTask) {}

  repositories {
    mavenCentral()
    google()
    jcenter()
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.8'
  distributionType = Wrapper.DistributionType.ALL
}
