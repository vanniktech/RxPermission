apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'

buildscript {
  ext.versions = [
    minSdk: 14,
    compileSdk: 28,
    targetSdk: 27,
    appcompat: '1.0.0',
    espresso: '3.1.0',
    testRules: '1.1.0',
    junitExt: '1.0.0',
    autoValue: '1.6.2',
  ]

  ext.deps = [
    'rx': [
      'java': 'io.reactivex.rxjava2:rxjava:2.2.3',
    ],
    'autoValue': [
      'compiler': "com.google.auto.value:auto-value:${versions.autoValue}",
      'runtime': "com.google.auto.value:auto-value-annotations:${versions.autoValue}",
    ],
    'support': [
      'appCompat': "androidx.appcompat:appcompat:${versions.appcompat}",

      'test': [
        'espresso': [
          'core': "androidx.test.espresso:espresso-core:${versions.espresso}",
          'web': "androidx.test.espresso:espresso-web:${versions.espresso}",
          'intents': "androidx.test.espresso:espresso-intents:${versions.espresso}",
        ],

        'runner': "androidx.test:runner:${versions.testRules}",
        'runnerExt': "androidx.test.ext:junit:${versions.junitExt}",
        'rules': "androidx.test:rules:${versions.testRules}",
        'uiautomator': 'androidx.test.uiautomator:uiautomator:2.2.0',
      ],
    ],
    'test': [
      'mockito': 'org.mockito:mockito-core:2.23.0',
      'junit': 'junit:junit:4.12',
      'assertj3': 'org.assertj:assertj-core:3.11.1',
    ],
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.13.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.13.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.6.0'

    classpath 'com.android.tools.build:gradle:3.3.0-beta03'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
  }
}

codeQualityTools {
  checkstyle {
    toolVersion = '8.7'
  }
  pmd {
    toolVersion = '6.0.1'
  }
  errorProne {
    toolVersion = '2.3.1'
  }
  lint {
    textReport = true
  }
}

subprojects {
  task allDependencies(type: DependencyReportTask) {}

  repositories {
    mavenCentral()
    google()
    jcenter()
  }
}

wrapper {
  gradleVersion = '4.10.2'
  distributionType = Wrapper.DistributionType.ALL
}
