apply plugin: 'com.vanniktech.code.quality.tools'

buildscript {
  ext.versions = [
    minSdk: 14,
    compileSdk: 31,
    targetSdk: 31,
    autoValue: '1.6.3',
    espresso: '3.4.0',
    testRules: '1.1.1',
  ]

  ext.deps = [
    'rx': [
      'java': 'io.reactivex.rxjava2:rxjava:2.2.21',
    ],
    'autoValue': [
      'compiler': "com.google.auto.value:auto-value:${versions.autoValue}",
      'runtime': "com.google.auto.value:auto-value-annotations:${versions.autoValue}",
    ],
    'support': [
      'appCompat': "androidx.appcompat:appcompat:1.4.2",
      'annotation': "androidx.annotation:annotation:1.4.0",

      'test': [
        'espresso': [
          'core': "androidx.test.espresso:espresso-core:${versions.espresso}",
          'web': "androidx.test.espresso:espresso-web:${versions.espresso}",
          'intents': "androidx.test.espresso:espresso-intents:${versions.espresso}",
        ],

        'junitExt': "androidx.test.ext:junit:1.1.3",
        'runner': "androidx.test:runner:${versions.testRules}",
        'rules': "androidx.test:rules:${versions.testRules}",
        'uiautomator': 'androidx.test.uiautomator:uiautomator:2.2.0',
      ],
    ],
    'test': [
      'mockito': 'org.mockito:mockito-core:4.6.1',
      'junit': 'junit:junit:4.13.2',
      'assertj3': 'org.assertj:assertj-core:3.23.1',
    ],
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:7.2.1'
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.21.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.20.0'
    classpath 'app.cash.licensee:licensee-gradle-plugin:1.4.1'
  }
}

codeQualityTools {
  checkstyle {
    enabled = false
  }
  pmd {
    enabled = false
  }
  cpd {
    enabled = false
  }
  lint {
    textReport = true
  }
  detekt {
    enabled = false
  }
  ktlint {
    version = "0.45.2"
  }
}

subprojects {
  task allDependencies(type: DependencyReportTask) {}

  repositories {
    mavenCentral()
    google()
    jcenter()
  }
}

wrapper {
  gradleVersion = '7.4.2'
  distributionType = Wrapper.DistributionType.ALL
}
