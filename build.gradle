apply plugin: 'com.vanniktech.code.quality.tools'

buildscript {
  ext.versions = [
    minSdk: 14,
    compileSdk: 33,
    targetSdk: 33,
    autoValue: '1.10.1',
    espresso: '3.5.0',
    testRules: '1.1.1',
  ]

  ext.deps = [
    'rx': [
      'java': 'io.reactivex.rxjava2:rxjava:2.2.21',
    ],
    'autoValue': [
      'compiler': "com.google.auto.value:auto-value:${versions.autoValue}",
      'runtime': "com.google.auto.value:auto-value-annotations:${versions.autoValue}",
    ],
    'support': [
      'appCompat': "androidx.appcompat:appcompat:1.5.1",
      'annotation': "androidx.annotation:annotation:1.5.0",

      'test': [
        'espresso': [
          'core': "androidx.test.espresso:espresso-core:${versions.espresso}",
          'web': "androidx.test.espresso:espresso-web:${versions.espresso}",
          'intents': "androidx.test.espresso:espresso-intents:${versions.espresso}",
        ],

        'junitExt': "androidx.test.ext:junit:1.1.4",
        'runner': "androidx.test:runner:${versions.testRules}",
        'rules': "androidx.test:rules:${versions.testRules}",
        'uiautomator': 'androidx.test.uiautomator:uiautomator:2.2.0',
      ],
    ],
    'test': [
      'mockito': 'org.mockito:mockito-core:4.9.0',
      'junit': 'junit:junit:4.13.2',
      'assertj3': 'org.assertj:assertj-core:3.23.1',
    ],
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:7.3.1'
    classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.21'
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.22.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.22.0'
    classpath 'app.cash.licensee:licensee-gradle-plugin:1.6.0'
    classpath 'com.dropbox.dependency-guard:dependency-guard:0.3.2'
  }
}

codeQualityTools {
  checkstyle {
    enabled = false
  }
  pmd {
    enabled = false
  }
  cpd {
    enabled = false
  }
  lint {
    textReport = true
  }
  detekt {
    enabled = false
  }
  ktlint {
    version = "0.47.1"
  }
}

subprojects {
  task allDependencies(type: DependencyReportTask) {}

  repositories {
    mavenCentral()
    google()
    jcenter()
  }
}
