apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'

buildscript {
  ext.versions = [
    minSdk: 14,
    compileSdk: 29,
    targetSdk: 29,
    autoValue: '1.6.3',
    espresso: '3.1.1',
    testRules: '1.1.1',
  ]

  ext.deps = [
    'rx': [
      'java': 'io.reactivex.rxjava3:rxjava:3.0.12',
    ],
    'autoValue': [
      'compiler': "com.google.auto.value:auto-value:${versions.autoValue}",
      'runtime': "com.google.auto.value:auto-value-annotations:${versions.autoValue}",
    ],
    'support': [
      'appCompat': "androidx.appcompat:appcompat:1.0.0",

      'test': [
        'espresso': [
          'core': "androidx.test.espresso:espresso-core:${versions.espresso}",
          'web': "androidx.test.espresso:espresso-web:${versions.espresso}",
          'intents': "androidx.test.espresso:espresso-intents:${versions.espresso}",
        ],

        'junitExt': "androidx.test.ext:junit:1.1.0",
        'runner': "androidx.test:runner:${versions.testRules}",
        'rules': "androidx.test:rules:${versions.testRules}",
        'uiautomator': 'androidx.test.uiautomator:uiautomator:2.2.0',
      ],
    ],
    'test': [
      'mockito': 'org.mockito:mockito-core:2.24.5',
      'junit': 'junit:junit:4.13',
      'assertj3': 'org.assertj:assertj-core:3.12.0',
    ],
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:4.2.0'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.28.0'

    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.20.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.16.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.8.0'

  }
}

codeQualityTools {
  checkstyle {
    toolVersion = '8.7'
  }
  pmd {
    toolVersion = '6.0.1'
  }
  errorProne {
    toolVersion = '2.3.1'
  }
  cpd {
    enabled = false
  }
  lint {
    textReport = true
  }
}

subprojects {
  task allDependencies(type: DependencyReportTask) {}

  repositories {
    mavenCentral()
    google()
    jcenter()
  }
}

wrapper {
  gradleVersion = '6.3'
  distributionType = Wrapper.DistributionType.ALL
}
